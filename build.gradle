plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.31"
    id "org.jetbrains.dokka" version "1.4.30"
}

group "org.example.dokka"
version "1.0-SNAPSHOT"

allprojects {
    apply plugin: "kotlin"
    apply plugin: "org.jetbrains.dokka"

    repositories {
        mavenCentral()
        maven { url = "https://dl.bintray.com/kotlin/dokka"}
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation project("submodule")
}

tasks.dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            moduleName.set("Dokka Multi-Module Example")
            includes.from("module.md", "packages.md")
        }
    }
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("${getRootDir()}/dokka"))
}

/**
 * Task that copies all .png files from the documentation folder to the build/javadoc/doc-files
 * folder so that they can be referenced in the generated documentation.
 */
task copyDocumentationImages(type: Copy){
    from ('images'){
        include '*.jpg'
    }
    into "${getRootDir()}/dokka/images"
}

/**
 * Task that will generate Dokka documentation and copy the images to the build
 * folder to be referenced.  This is a workaround since Dokka does not currently support
 * embedding images.
 */
task generateDocumentation(){
    dependsOn 'dokkaHtmlMultiModule'
    dependsOn 'copyDocumentationImages'
}
